File -> TopLevel Expression? ';'?

TopLevel -> TopLevel Function
          | TopLevel Type
          | TopLevel Protocol
          | <epsilon>

Function -> 'function' Prototype FunctionBlock

Prototype -> Identifier '(' Params ')' TypeAnnotation?

FunctionBlock -> BlockExpression
               | '=>' Expression ';'

Params -> Params ',' Binding
        | Binding
        | <epsilon>

Binding -> Identifier TypeAnnotation?

TypeAnnotation -> ':' Identifier

Type -> 'type' Constructor Inheritance? TypeBlock

Constructor -> Identifier '(' Params ')'
             | Identifier

Inheritance -> 'inherits' BaseType

BaseType -> Identifier
          | CallExpression

TypeBlock -> '{' Members '}'

Members -> Members Assignment ';'
         | Members Prototype FunctionBlock
         | <epsilon>

Assignment -> Binding '=' Expression

Protocol -> 'protocol' Identifier Extends? ProtocolBlock

Extends -> 'extends' Identifier

ProtocolBlock -> '{' Methods '}'

Methods -> Methods Prototype ';'
         | <epsilon>

Expression -> StructuredExpression
            | BasicExpression

BasicExpression -> BasicExpression '&' BoolExpression
                 | BasicExpression '|' BoolExpression
                 | BoolExpression

BoolExpression -> '!' BoolExpression
                | Predicate

Predicate -> ArithExpression '<' ArithExpression
           | ArithExpression '<=' ArithExpression
           | ArithExpression '>' ArithExpression
           | ArithExpression '>=' ArithExpression
           | ArithExpression '==' ArithExpression
           | ArithExpression '!=' ArithExpression
           | TypeTestExpression
           | ArithExpression

ArithExpression -> ArithExpression '+' Term
                 | ArithExpression '-' Term
                 | ArithExpression '@''@'? Term
                 | Term

Term -> Term '*' UnarySub
      | Term '/' UnarySub
      | Term '%' UnarySub
      | UnarySub

UnarySub -> '-' Factor
          | Factor

Factor -> PrimitiveExpression '^' Factor
        | PrimitiveExpression

StructuredExpression -> BlockExpression
                      | LetExpression
                      | DestructiveAssignment
                      | ConditionalExpression
                      | WhileExpression
                      | ForExpression
                      | DowncastExpression
                      | TypeInstantiation

BlockExpression -> '{' ExpressionList '}' ';'?

ExpressionList -> ExpressionList Expression ';'
                | <epsilon>

LetExpression -> 'let' AssignmentList 'in' Expression

AssignmentList -> AssignmentList ',' Assignment
                | Assignment

DestructiveAssignment -> Access ':=' Expression

ConditionalExpression -> 'if' '(' Expression ')' Expression ChainedConditional

ChainedConditional -> 'elif' '(' Expression ')' Expression ChainedConditional
                    | 'else' Expression

WhileExpression -> 'while' '(' Expression ')' Expression

ForExpression -> 'for' '(' Iterator ')' Expression

Iterator -> Binding 'in' Expression

TypeTestExpression -> ArithExpression 'is' Identifier

DowncastExpression -> ArithExpression 'as' Identifier

TypeInstantiation -> 'new' Identifier '(' Arguments ')'

Arguments -> Arguments ',' Expression
           | Expression
           | <epsilon>

PrimitiveExpression -> '(' Expression ')'
                     | NumericLiteral
                     | StringLiteral
                     | BoolLiteral
                     | VectorLiteral
                     | CallExpression
                     | Access

NumericLiteral -> [0-9]+|[0-9]*.[0-9]+

StringLiteral -> '"' ([^"]|\"|\n\t)* '"'

Identifier -> [_a-zA-Z][_a-zA-Z0-9]*

BoolLiteral -> 'true'
             | 'false'

VectorLiteral -> '[' Arguments ']'
               | '[' Expression '||' Iterator ']'

CallExpression -> Access '(' Arguments ')'

Access -> Access '.' Identifier
        | Access '[' Expression ']'
        | Identifier
